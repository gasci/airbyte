import org.gradle.api.Plugin
import org.gradle.api.Project

class AirbyteBulkConnectorExtension {

    String core // 'extract' or 'load'
    List<String> toolkits = []  // 'extract-jdbc', etc...
    String cdk // 'local' or a specific version

    AirbyteBulkConnectorExtension(Project project) {
        this.project = project
    }

    private Project project

    void setCore(String core) {
        this.core = core
        if (core != "extract" && core != "load") {
            throw new IllegalArgumentException("'core' should be either 'extract' or 'load'")
        }
        if (cdk != null) {
            throw new IllegalArgumentException("'core' should be defined before 'cdk'")
        }
    }

    void setToolkits(List<String> toolkits) {
        this.toolkits = toolkits
        if (cdk != null) {
            throw new IllegalArgumentException("'toolkits' should be defined before 'cdk'")
        }
    }

    void setCdk(String cdk) {
        this.cdk = cdk
        if (cdk != "local" && !(cdk.matches("^[0-9]+\\.[0-9]+") || cdk.matches("^[0-9]+\\.[0-9]+\\.[0-9]+"))) {
            throw new IllegalArgumentException("'cdk' should be either a well-formed version number or 'local'")
        }

        project.processResources {
            // The metadata.yaml file is required by Micronaut.
            from(project.projectDir) {
                include 'metadata.yaml'
                duplicatesStrategy DuplicatesStrategy.EXCLUDE
            }
        }

        List<Project> fromSource = []
        List<String> fromJar = []
        if (cdk == "local") {
            fromSource = [
                    project.project(":airbyte-cdk:bulk:core:bulk-cdk-core-base"),
                    project.project(":airbyte-cdk:bulk:core:bulk-cdk-core-$core"),
            ]
            for (toolkit in toolkits) {
                if (toolkit.contains(':')) {
                    fromJar << "io.airbyte.bulk-cdk:bulk-cdk-toolkit-$toolkit"
                } else {
                    fromSource << project.project(":airbyte-cdk:bulk:toolkits:bulk-cdk-toolkit-$toolkit")
                }
            }
        } else {
            fromJar = [
                    "io.airbyte.bulk-cdk:bulk-cdk-core-base:$cdk",
                    "io.airbyte.bulk-cdk:bulk-cdk-core-$core:$cdk",
            ]
            for (toolkit in toolkits) {
                if (toolkit.contains(':')) {
                    fromJar << "io.airbyte.bulk-cdk:bulk-cdk-toolkit-$toolkit"
                } else {
                    fromJar << "io.airbyte.bulk-cdk:bulk-cdk-toolkit-$toolkit:$cdk"
                }
            }
        }

        String micronautVersion = null
        // Use a temporary configuration to find the Micronaut version
        // in the transitive dependencies of the Bulk CDK base.
        def tempConfiguration = project.configurations.detachedConfiguration()
        def baseDependency = (cdk == "local")
                ? project.dependencies.create(fromSource.first)
                : project.dependencies.create(fromJar.first)
        tempConfiguration.dependencies.add(baseDependency)
        tempConfiguration.resolvedConfiguration.firstLevelModuleDependencies.each {resolvedDependency ->
            resolvedDependency.children.each { transitiveDependency ->
                if (micronautVersion == null &&
                        transitiveDependency.moduleGroup == TARGET_GROUP &&
                        transitiveDependency.moduleName == TARGET_MODULE) {
                    micronautVersion = transitiveDependency.moduleVersion
                }
            }
        }
        if (micronautVersion == null) {
            throw new IllegalArgumentException(
                    "Micronaut version number not found; $TARGET_GROUP:$TARGET_MODULE is not a transitive dependency."
            )
        }
        final String kspDependency = "io.micronaut:micronaut-inject-kotlin:$micronautVersion"

        final boolean isDestination = project.name.startsWith('destination-')
        project.dependencies {
            ksp kspDependency
            kspTestFixtures kspDependency
            kspTest kspDependency

            for (dep in fromSource) {
                implementation dep
                testFixturesImplementation dep
                testFixturesImplementation testFixtures(dep)
                testImplementation dep
                testImplementation testFixtures(dep)
                if (isDestination) {
                    integrationTestImplementation dep
                    integrationTestImplementation testFixtures(dep)
                    integrationTestLegacyImplementation dep
                    integrationTestLegacyImplementation testFixtures(dep)
                }
            }
            for (dep in fromJar) {
                implementation dep
                testFixturesImplementation dep
                testFixturesImplementation testFixtures(dep)
                testImplementation dep
                testImplementation testFixtures(dep)
                if (isDestination) {
                    integrationTestImplementation dep
                    integrationTestImplementation testFixtures(dep)
                    integrationTestLegacyImplementation dep
                    integrationTestLegacyImplementation testFixtures(dep)
                }
            }
        }
    }

    static private final String TARGET_GROUP = "io.micronaut"
    static private final String TARGET_MODULE = "micronaut-runtime"
}

class AirbyteBulkConnectorPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {

        project.plugins.apply('application')

        project.application {
            applicationDefaultJvmArgs = [
                    '-XX:+ExitOnOutOfMemoryError',
                    '-XX:InitialRAMPercentage=75.0',
                    '-XX:MaxRAMPercentage=75.0',
            ]
        }

        if (project.name.startsWith('destination-')) {
            project.sourceSets {
                // This is the source set where most of our integration tests should live.
                integrationTest {
                    kotlin {
                        srcDir 'src/test-integration/kotlin'
                    }
                    resources {
                        srcDir 'src/test-integration/resources'
                    }
                }
                // This source set should only be used for tests based on the old CDK's test classes,
                // in particular DestinationAcceptanceTest / BaseTypingDedupingTest.
                integrationTestLegacy {
                    kotlin {
                        srcDir 'src/test-integration-legacy/kotlin'
                    }
                    resources {
                        srcDir 'src/test-integration-legacy/resources'
                    }
                }
            }

            // This task exists solely so that we can run the "modern" integration tests
            // in non-docker mode on our laptops.
            // It's not a dependency of any other task.
            project.tasks.register('integrationTestNonDocker', Test) {
                description = 'Runs the integration tests in non-docker mode.'
                group = 'verification'
                testClassesDirs = project.sourceSets.integrationTest.output.classesDirs
                classpath = project.sourceSets.integrationTest.runtimeClasspath
                useJUnitPlatform()

                jvmArgs = project.test.jvmArgs
                systemProperties = project.test.systemProperties
                maxParallelForks = project.test.maxParallelForks
                maxHeapSize = project.test.maxHeapSize

                testLogging() {
                    events 'skipped', 'started', 'passed', 'failed'
                    exceptionFormat 'full'
                    showStandardStreams = true
                }

                // Always re-run integration tests no matter what.
                outputs.upToDateWhen { false }
            }

            // For historical reasons (i.e. airbyte-ci), this task is called integrationTestJava.
            // airbyte-ci only wants to run a task with exactly that name.
            // There's intentionally no equivalent to run integrationTestNonDocker + integrationTestLegacy.
            // It's an actual Test task (rather than just depending on two sub-tasks integrationTestDocker + integrationTestLegacy)
            // so that gradle args (e.g. `--tests`) work as expected.
            project.tasks.register('integrationTestJava', Test) {
                description = 'Runs the integration tests in docker mode.'
                group = 'verification'
                testClassesDirs = project.sourceSets.integrationTest.output.classesDirs + project.sourceSets.integrationTestLegacy.output.classesDirs
                classpath = project.sourceSets.integrationTest.runtimeClasspath + project.sourceSets.integrationTestLegacy.runtimeClasspath
                useJUnitPlatform()
                // We need a docker image to run this task, so depend on assemble
                dependsOn project.tasks.assemble
                environment "AIRBYTE_CONNECTOR_INTEGRATION_TEST_RUNNER", "docker"

                jvmArgs = project.test.jvmArgs
                systemProperties = project.test.systemProperties
                maxParallelForks = project.test.maxParallelForks
                maxHeapSize = project.test.maxHeapSize

                testLogging() {
                    events 'skipped', 'started', 'passed', 'failed'
                    exceptionFormat 'full'
                    showStandardStreams = true
                }

                // Always re-run integration tests no matter what.
                outputs.upToDateWhen { false }
            }

            project.dependencies {
                // integration tests depend on the runtime code
                integrationTestImplementation project
                integrationTestLegacyImplementation project
                integrationTestLegacyImplementation project.sourceSets.integrationTest.output
            }

            project.tasks.named('build').configure {
                dependsOn project.tasks.integrationTestJava
            }
        }

        project.tasks.matching {
                it.name == 'spotbugsIntegrationTestLegacy' ||
                it.name == 'spotbugsIntegrationTest' ||
                it.name == 'spotbugsTest'
            }.configureEach {
            enabled = false
        }

        project.tasks.named('check').configure {
            dependsOn project.tasks.matching { it.name ==~ /(compile|spotbugs)[a-zA-Z]*Java/ }
        }

        boolean withSlowTests = System.getProperty('skipSlowTests', 'false') == 'false'
        project.tasks.matching {
                it.name == 'test' ||
                it.name == 'integrationTestNonDocker' ||
                it.name == 'integrationTestJava'
            }.configureEach {
            onlyIf { withSlowTests }
        }

        project.configurations {
            testFixturesImplementation.extendsFrom implementation
            testFixturesRuntimeOnly.extendsFrom runtimeOnly
        }

        project.tasks.named('test').configure {
            // enable the "don't be evil" option, which for some reason is disabled by default
            systemProperties(["mockk.junit.extension.requireParallelTesting": "true"])
        }

        def extension = project.extensions.create('airbyteBulkConnector', AirbyteBulkConnectorExtension, project)

        project.tasks.register('getCdkVersion') {
            description = 'Gets the CDK version used by the connector'
            group = 'airbyte-bulk-connector'

            doLast {
                println extension.cdk
            }
        }

        project.tasks.register('upgradeCdk') {
            description = 'Upgrades the CDK version used by the connector. If no version is specified, upgrades to the local CDK version.'
            group = 'airbyte-bulk-connector'

            doLast {
                String targetVersion = project.hasProperty('cdkVersion') ? project.property('cdkVersion') : null

                if (targetVersion == null) {
                    targetVersion = getLocalCdkVersion(project)
                    println "No version specified, upgrading to local CDK version: $targetVersion"
                } else {
                    println "Version specified, using the provided CDK versionn: $targetVersion"
                }

                updateCdkVersion(project, targetVersion)
            }
        }
    }

    private static String getLocalCdkVersion(Project project) throws IOException {
        File versionPropsFile = new File(project.rootDir, 'airbyte-cdk/bulk/core/base/src/main/resources/version.properties')
        if (!versionPropsFile.exists()) {
            throw new FileNotFoundException("CDK version file not found: ${versionPropsFile.absolutePath}")
        }

        Properties props = new Properties()
        versionPropsFile.withInputStream { stream -> props.load(stream) }
        if (!props.containsKey('version')) {
            throw new NoSuchElementException("Missing 'version' property in ${versionPropsFile.absolutePath}")
        }

        String localVersion = props.getProperty('version')?.trim()
        if (!localVersion) {
            throw new IllegalStateException("'version' property is empty in ${versionPropsFile.absolutePath}")
        }

        project.logger.info("Found local CDK version from version.properties: ${localVersion}")
        return localVersion
    }

    private static void updateCdkVersion(Project project, String version) {
        File buildFile = project.buildFile
        String content = buildFile.text

        // regex that extracts the CDK version from the airbyteBulkConnector block
        def pattern = /(?s)(airbyteBulkConnector\s*\{.*?cdk\s*=\s*['"])([^'"]+)(['"].*?\})/

        def matcher = content =~ pattern
        if (matcher.find()) {
            String updatedContent = content.replaceAll(pattern, "\$1${version}\$3")
            buildFile.text = updatedContent
            project.logger.info("Updated CDK version from '${matcher.group(2)}' to '${version}' in ${buildFile.absolutePath}")
        } else {
            throw new NoSuchElementException("No CDK version configuration found in ${buildFile.absolutePath}. " +
                    "Please ensure the connector uses airbyteBulkConnector plugin" +
                    "and has a 'cdk' property set.")
        }
    }
}
